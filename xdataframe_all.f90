module kind_mod
implicit none
private
public :: dp
integer, parameter :: dp = kind(1.0d0)
end module kind_mod
module util_mod
use iso_fortran_env, only: output_unit
use kind_mod, only: dp
implicit none
private
public :: default, assert_equal, write_merge, split_string, display, &
   print_time_elapsed, read_words_line, str, print_table, exe_name, &
   join, seq, cbind
interface default
   module procedure default_int, default_real, default_logical, &
      default_character
end interface default
interface seq
   module procedure seq_stride, seq_unit_stride
end interface seq
interface cbind
   module procedure cbind_vec_vec, cbind_mat_vec, cbind_mat_mat
end interface cbind
interface display
   module procedure display_matrix, display_vector
end interface display
contains

elemental function default_int(x, xopt) result(y)
! return xopt if present, otherwise x
integer, intent(in) :: x
integer, intent(in), optional :: xopt
integer             :: y
if (present(xopt)) then
   y = xopt
else
   y = x
end if
end function default_int

elemental function default_real(x, xopt) result(y)
! return xopt if present, otherwise x
real(kind=dp), intent(in) :: x
real(kind=dp), intent(in), optional :: xopt
real(kind=dp)             :: y
if (present(xopt)) then
   y = xopt
else
   y = x
end if
end function default_real

elemental function default_logical(x, xopt) result(y)
! return xopt if present, otherwise x
logical, intent(in) :: x
logical, intent(in), optional :: xopt
logical             :: y
if (present(xopt)) then
   y = xopt
else
   y = x
end if
end function default_logical

elemental function default_character(x, xopt) result(y)
! return xopt if present, otherwise x
character (len=*), intent(in) :: x
character (len=*), intent(in), optional :: xopt
character (len=100) :: y
if (present(xopt)) then
   y = xopt
else
   y = x
end if
end function default_character

subroutine assert_equal(k, kreq, msg)
! check that k == kreq
integer, intent(in) :: k, kreq
character (len=*), intent(in) :: msg
if (k /= kreq) then
   print "(a, i0, a, i0)", msg // " = ", k, ", must equal ", kreq
   stop
end if
end subroutine assert_equal

subroutine write_merge(tf, x, y, outu, fmt)
!> Writes either `x` or `y` to the specified output unit using the given format.
!! If `tf` is true, writes `x`; otherwise, writes `y`.
!! @param tf Logical condition determining whether to write `x` or `y`.
!! @param x The first character string to write if `tf` is true.
!! @param y The second character string to write if `tf` is false.
!! @param outu Optional output unit (defaults to a predefined output unit).
!! @param fmt Optional format specifier (defaults to "(a)").
logical, intent(in) :: tf
character (len=*), intent(in) :: x, y
integer, intent(in), optional :: outu
character (len=*), intent(in), optional :: fmt
integer :: outu_
character (len=100) :: fmt_
outu_ = default(output_unit, outu)
if (present(fmt)) then
   fmt_ = fmt
else
   fmt_ = "(a)"
end if
if (tf) then
   write (outu_, fmt_) x
else
   write (outu_, fmt_) y
end if
end subroutine write_merge

!------------------------------------------------------------------
! Utility: split_string
!
! Splits the input string 'str' at each occurrence of the single-
! character delimiter 'delim' and returns the pieces in the allocatable
! array 'tokens'. To allocate each element (with deferred length)
! properly, we use the length of the input string.
!------------------------------------------------------------------
subroutine split_string(str, delim, tokens)
character(len=*), intent(in)           :: str
character(len=*), intent(in)           :: delim
character(:), allocatable, intent(out) :: tokens(:)
integer :: start, pos, i, count, n

n = len_trim(str)
if (n == 0) then
   allocate(character(len=0) :: tokens(1))
   tokens(1) = ""
   return
end if

! First pass: count tokens.
count = 0
start = 1
do
   pos = index(str(start:), delim)
   if (pos == 0) then
      count = count + 1
      exit
   else
      count = count + 1
      start = start + pos
   end if
end do

! Allocate tokens; each token gets the full length of the input.
allocate(character(len=n) :: tokens(count))

! Second pass: extract tokens.
start = 1
i = 1
do
   pos = index(str(start:), delim)
   if (pos == 0) then
      tokens(i) = adjustl(str(start:))
      exit
   else
      tokens(i) = adjustl(str(start:start+pos-2))
      start = start + pos
      i = i + 1
   end if
end do
end subroutine split_string

subroutine display_matrix(x, outu, fmt_r, fmt_header, fmt_trailer, &
   title)
! print a matrix
real(kind=dp)    , intent(in)           :: x(:,:)
integer          , intent(in), optional :: outu
character (len=*), intent(in), optional :: fmt_r, fmt_header, &
                                           fmt_trailer, title
integer                                 :: i, outu_
character (len=100)                     :: fmt_r_
outu_  = default(output_unit, outu)
fmt_r_ = default("(*(1x,f10.4))", fmt_r)
if (present(fmt_header)) write(outu_, fmt_header)
if (present(title)) write (outu_, "(a)") title
do i=1,size(x,1)
   write(outu_,fmt_r_) x(i,:)
end do
if (present(fmt_trailer)) write(outu_, fmt_trailer)
end subroutine display_matrix

subroutine display_vector(x, outu, fmt_r, fmt_header, title)
! print a vector
real(kind=dp)    , intent(in)           :: x(:)
integer          , intent(in), optional :: outu
character (len=*), intent(in), optional :: fmt_r, fmt_header, title
integer                                 :: i, outu_
character (len=100)                     :: fmt_r_
outu_  = default(output_unit, outu)
fmt_r_ = default("(*(1x,f10.4))", fmt_r)
if (present(fmt_header)) write(outu_, fmt_header)
if (present(title)) write (outu_, "(a)") title
do i=1,size(x)
   write(outu_,fmt_r_) x(i)
end do
end subroutine display_vector

subroutine print_time_elapsed(old_time, outu)
real(kind=dp), intent(in) :: old_time ! previously set by call cpu_time(old_time)
real(kind=dp)             :: tt
integer      , intent(in), optional :: outu
integer                             :: outu_
character (len=100) :: fmt_time_
outu_ = default(output_unit, outu)
call cpu_time(tt)
fmt_time_= "('time elapsed (s): ', f0.4)"
write (outu_, fmt_time_) tt - old_time
end subroutine print_time_elapsed

subroutine read_words_line(iu,words)
! read words from line, where the line has the # of words followed by the words
! n word_1 word_2 ... word_n
integer          , intent(in)               :: iu
character (len=*), intent(out), allocatable :: words(:)
integer :: ierr, nwords
character (len=10000) :: text
read (iu,"(a)") text
read (text, *) nwords
allocate (words(nwords))
read (text, *, iostat=ierr) nwords, words
if (ierr /= 0) then
   print*,"could not read ", nwords, " words from '" // trim(text) // "'"
   error stop
end if
end subroutine read_words_line

function str(i) result(text)
! convert integer to string
integer, intent(in) :: i
character (len=20) :: text
write (text,"(i0)") i
end function str

subroutine print_table(x, row_names, col_names, outu, &
   fmt_col_names, fmt_row, fmt_header, fmt_trailer)
! print a table with row and column names
real(kind=dp)    , intent(in) :: x(:,:) ! matrix to be printed
character (len=*), intent(in) :: row_names(:), col_names(:)
integer          , intent(in), optional :: outu ! output unit
character (len=*), intent(in), optional :: fmt_col_names, fmt_row, &
   fmt_header, fmt_trailer
integer                       :: i, n1, n2, outu_
character (len=*), parameter  :: msg="in print_table, "
character (len=100) :: fmt_col_names_, fmt_row_
n1 = size(x, 1)
n2 = size(x, 2)
call assert_equal(size(row_names), n1, msg // "size(row_names)")
call assert_equal(size(col_names), n2, msg // "size(col_names)")
fmt_col_names_ = default("(*(a12,:,1x))", fmt_col_names)
fmt_row_ = default("(a12, *(1x,f12.6))", fmt_row)
outu_ = default(output_unit, outu)
if (present(fmt_header)) write (outu_, fmt_header)
write (outu_, fmt_col_names_) "", (trim(col_names(i)), i=1,n2)
do i=1,n1
   write (outu_, fmt_row_) trim(row_names(i)), x(i,:)
end do
if (present(fmt_trailer)) write (outu_, fmt_trailer)
end subroutine print_table

function exe_name() result(xname)
! return the program name
character (len=1000) :: xname
call get_command_argument(0,xname)
xname = trim(xname)
end function exe_name

function join(words,sep) result(str)
! trim and concatenate a vector of character variables,
! inserting sep between them
character (len=*), intent(in)                                   :: words(:),sep
character (len=(size(words)-1)*len(sep) + sum(len_trim(words))) :: str
integer                                                         :: i,nw
nw  = size(words)
str = ""
if (nw < 1) then
   return
else
   str = words(1)
end if
do i=2,nw
   str = trim(str) // sep // words(i) 
end do
end function join

pure function seq_stride(first, last, stride) result(vec)
!! return an integer sequence from first through last
integer, intent(in) :: first, last, stride
integer, allocatable :: vec(:)
integer :: i, n, idiff
idiff = last - first
n = max(0, 1 + idiff/stride)
allocate (vec(n))
do i=1, n
   vec(i) = first + (i - 1) * stride
end do
end function seq_stride

pure function seq_unit_stride(first, last) result(vec)
!! return an integer sequence from first through last
integer, intent(in) :: first, last
integer, allocatable :: vec(:)
integer :: i, n
n = max(0, last - first + 1)
allocate (vec(n))
do i=1, n
   vec(i) = first + i - 1
end do
end function seq_unit_stride

pure function cbind_vec_vec(x,y) result(xy)
! return a matrix whose columns are x(:) and y(:)
real(kind=dp), intent(in) :: x(:), y(:)
real(kind=dp), allocatable :: xy(:,:)
integer :: n
n = size(x,1)
if (size(y) /= n) error stop "mismatched sizes in cbind"
xy = reshape([x, y], [n, 2])
end function cbind_vec_vec

pure function cbind_mat_vec(x,y) result(xy)
! append vector y(:) to matrix x(:,:)
real(kind=dp), intent(in) :: x(:,:), y(:)
real(kind=dp), allocatable :: xy(:,:)
integer :: n1, n2
n1 = size(x,1)
if (size(y) /= n1) error stop "mismatched sizes in cbind"
n2 = size(x,2)
allocate (xy(n1, n2+1))
xy(:,:n2)  = x
xy(:,n2+1) = y 
end function cbind_mat_vec

pure function cbind_mat_mat(x,y) result(xy)
! append columns of y(:,:) to matrix x(:,:)
real(kind=dp), intent(in) :: x(:,:), y(:,:)
real(kind=dp), allocatable :: xy(:,:)
integer :: n1, n2
n1 = size(x,1)
if (size(y,1) /= n1) error stop "mismatched sizes in cbind"
n2 = size(x,2)
allocate (xy(n1, n2+size(y,2)))
xy(:,:n2)  = x
xy(:,n2+1:) = y 
end function cbind_mat_mat

! function appended_char_vec(x, y) result(xy)
! character (len=*), intent(in) :: x(:)
! character (len=*), intent(in) :: y
! character (len=len(x)), allocatable :: xy(:)
!  
! end function appended_char_vec

end module util_mod
module dataframe_mod
use kind_mod, only: dp
use util_mod, only: default, split_string, seq, cbind
use iso_fortran_env, only: output_unit
use, intrinsic :: ieee_arithmetic, only: ieee_value, ieee_quiet_nan, ieee_is_nan
implicit none
private
public :: DataFrame, nrow, ncol, print_summary, random, operator(*), &
   operator(/), operator(+), operator(-), display, allocate_df, &
   operator(**), shape, subset_stride
integer, parameter :: nlen_columns = 100, nrows_print = 10 ! number of rows to print by default.
logical, save :: blank_line_before_display = .true.
interface display
   module procedure display_data
end interface display
interface operator (*)
   module procedure mult_x_df, mult_df_x, mult_n_df, mult_df_n
   module procedure mult_df_df
end interface
interface operator (/)
   module procedure div_df_x, div_df_n, div_x_df, div_n_df
   module procedure div_df_df
end interface
interface operator (+)
   module procedure add_x_df, add_df_x, add_n_df, add_df_n
   module procedure add_df_df
end interface
interface operator (-)
   module procedure subtract_x_df, subtract_df_x, &
      subtract_n_df, subtract_df_n, subtract_df_df
end interface
interface operator (**)
   module procedure power_df_n, power_df_x
end interface

type :: DataFrame
   integer, allocatable          :: index(:)
   character(len=nlen_columns), allocatable :: columns(:)
   real(kind=dp), allocatable    :: values(:,:)
   contains
      procedure :: read_csv, display=>display_data, write_csv, irow, icol, &
         loc, append_col, append_cols, set_col, col_pos, row_pos, sort_index, is_sorted_index, is_unique_index, at, iat, set_at, set_iat, &
         has_col, has_idx, drop_cols, drop_rows, rename_cols, where_cols, filter_cols, where, filter, iloc, select, add, subtract, multiply, divide, &
         reindex, shift, pct_change, log_change
end type DataFrame

contains

pure function shape(df) result(ishape)
! return a 2-element array with the number of rows and columns of the dataframe
type(DataFrame), intent(in) :: df
integer                     :: ishape(2)
ishape = [nrow(df), ncol(df)]
end function shape

pure function icol(df, ivec) result(df_new)
! returns a dataframe with the subset of columns in ivec(:)
class(DataFrame), intent(in) :: df
integer, intent(in) :: ivec(:)
type(DataFrame) :: df_new
df_new = DataFrame(index=df%index, columns=df%columns(ivec), values=df%values(:, ivec))
end function icol

pure function loc(df, rows, columns) result(df_new)
! return a subset of a dataframe with the specified rows (index values) and columns
class(DataFrame), intent(in) :: df
integer, intent(in), optional :: rows(:)
character (len=*), intent(in), optional :: columns(:)
type(DataFrame) :: df_new
integer, allocatable :: rows_(:)
character (len=nlen_columns), allocatable :: columns_(:)
integer :: i
integer, allocatable :: jrow(:), jcol(:)
if (present(rows)) then
   rows_ = rows
   allocate (jrow(size(rows)))
   do i=1,size(rows)
      jrow(i) = findloc(df%index, rows(i), dim=1)
   end do
else
   rows_ = df%index
   jrow = seq(1, nrow(df))
end if
if (present(columns)) then
   columns_ = columns
   allocate(jcol(size(columns)))
   do i=1,size(columns)
      jcol(i) = findloc(df%columns, columns(i), dim=1)
   end do
else
   columns_ = df%columns
   jcol = seq(1, ncol(df))
end if
df_new = DataFrame(index=rows_, columns=columns_, values=df%values(jrow, jcol))
end function loc

pure function row_pos(self, idx, assume_sorted, ascending) result(irow)
! return the row position (1..nrow) for index value idx
! if assume_sorted is true, use binary search assuming index is sorted
class(DataFrame), intent(in) :: self
integer, intent(in) :: idx
logical, intent(in), optional :: assume_sorted, ascending
integer :: irow
logical :: do_sorted, asc
integer :: lo, hi, mid

do_sorted = default(.false., assume_sorted)
asc = default(.true., ascending)

if (.not. allocated(self%index)) error stop "in row_pos, index is not allocated"

if (do_sorted) then
   ! binary search for first occurrence (like findloc) in a sorted index
   lo = 1
   hi = size(self%index)
   irow = 0
   do while (lo <= hi)
      mid = (lo + hi) / 2
      if (asc) then
         if (self%index(mid) < idx) then
            lo = mid + 1
         else
            if (self%index(mid) == idx) irow = mid
            hi = mid - 1
         end if
      else
         if (self%index(mid) > idx) then
            lo = mid + 1
         else
            if (self%index(mid) == idx) irow = mid
            hi = mid - 1
         end if
      end if
   end do
else
   irow = findloc(self%index, idx, dim=1)
end if

if (irow == 0) error stop "in row_pos, index not found"
end function row_pos

function is_sorted_index(self, ascending) result(is_sorted)
! return true if index is sorted (nondecreasing if ascending, nonincreasing otherwise)
class(DataFrame), intent(in) :: self
logical, intent(in), optional :: ascending
logical :: is_sorted
logical :: asc
integer :: i, n

asc = default(.true., ascending)

if (.not. allocated(self%index)) then
   is_sorted = .true.
   return
end if

n = size(self%index)
is_sorted = .true.
if (n <= 1) return

if (asc) then
   do i=2,n
      if (self%index(i) < self%index(i-1)) then
         is_sorted = .false.
         exit
      end if
   end do
else
   do i=2,n
      if (self%index(i) > self%index(i-1)) then
         is_sorted = .false.
         exit
      end if
   end do
end if
end function is_sorted_index

function is_unique_index(self) result(is_unique)
! return true if index has no duplicates
class(DataFrame), intent(in) :: self
logical :: is_unique
integer :: n, i
integer, allocatable :: perm(:), tmp(:)

if (.not. allocated(self%index)) then
   is_unique = .true.
   return
end if

n = size(self%index)
if (n <= 1) then
   is_unique = .true.
   return
end if

! sort a copy and check adjacent duplicates
allocate(tmp(n))
tmp = self%index
call argsort_int(tmp, perm, ascending=.true.)
tmp = tmp(perm)
is_unique = .true.
do i=2,n
   if (tmp(i) == tmp(i-1)) then
      is_unique = .false.
      exit
   end if
end do
deallocate(tmp, perm)
end function is_unique_index

subroutine sort_index(self, ascending)
! sort rows by index, permuting values accordingly
class(DataFrame), intent(inout) :: self
logical, intent(in), optional :: ascending
logical :: asc
integer :: n
integer, allocatable :: perm(:)
real(kind=dp), allocatable :: vtmp(:,:)

asc = default(.true., ascending)

if (.not. allocated(self%index)) return
if (.not. allocated(self%values)) return

n = size(self%index)
if (n <= 1) return

call argsort_int(self%index, perm, ascending=asc)

! reorder index
self%index = self%index(perm)

! reorder values
allocate(vtmp(n, size(self%values,2)))
vtmp = self%values(perm, :)
self%values = vtmp
deallocate(vtmp, perm)
end subroutine sort_index

subroutine argsort_int(a, perm, ascending)
! return permutation perm such that a(perm) is sorted (stable mergesort)
integer, intent(in) :: a(:)
integer, allocatable, intent(out) :: perm(:)
logical, intent(in), optional :: ascending
logical :: asc
integer :: n, width, i, left, mid, right, p, q, k
integer, allocatable :: tmp(:)

asc = default(.true., ascending)
n = size(a)
allocate(perm(n), tmp(n))
perm = [(i, i=1,n)]

width = 1
do while (width < n)
   i = 1
   do while (i <= n)
      left = i
      mid = min(i + width - 1, n)
      right = min(i + 2*width - 1, n)

      p = left
      q = mid + 1
      k = left

      do while (p <= mid .and. q <= right)
         if (asc) then
            if (a(perm(p)) <= a(perm(q))) then
               tmp(k) = perm(p)
               p = p + 1
            else
               tmp(k) = perm(q)
               q = q + 1
            end if
         else
            if (a(perm(p)) >= a(perm(q))) then
               tmp(k) = perm(p)
               p = p + 1
            else
               tmp(k) = perm(q)
               q = q + 1
            end if
         end if
         k = k + 1
      end do

      do while (p <= mid)
         tmp(k) = perm(p)
         p = p + 1
         k = k + 1
      end do

      do while (q <= right)
         tmp(k) = perm(q)
         q = q + 1
         k = k + 1
      end do

      perm(left:right) = tmp(left:right)
      i = i + 2*width
   end do
   width = 2*width
end do

deallocate(tmp)
end subroutine argsort_int


pure function col_pos(self, column) result(jcol)
! return the column position (1..ncol) for column name
class(DataFrame), intent(in) :: self
character(len=*), intent(in) :: column
integer :: jcol
jcol = findloc(self%columns, column, dim=1)
if (jcol == 0) error stop "in col_pos, column not found: " // trim(column)
end function col_pos

pure function iat(self, i, j) result(x)
! return a scalar element by 1-based row/column positions
class(DataFrame), intent(in) :: self
integer, intent(in) :: i, j
real(kind=dp) :: x
if (i < 1 .or. i > nrow(self)) error stop "in iat, row position out of range"
if (j < 1 .or. j > ncol(self)) error stop "in iat, column position out of range"
x = self%values(i, j)
end function iat

pure function at(self, idx, column) result(x)
! return a scalar element by index value and column name
class(DataFrame), intent(in) :: self
integer, intent(in) :: idx
character(len=*), intent(in) :: column
real(kind=dp) :: x
integer :: i, j
i = self%row_pos(idx)
j = self%col_pos(column)
x = self%values(i, j)
end function at

pure subroutine set_iat(self, i, j, x)
! set a scalar element by 1-based row/column positions
class(DataFrame), intent(in out) :: self
integer, intent(in) :: i, j
real(kind=dp), intent(in) :: x
if (i < 1 .or. i > nrow(self)) error stop "in set_iat, row position out of range"
if (j < 1 .or. j > ncol(self)) error stop "in set_iat, column position out of range"
self%values(i, j) = x
end subroutine set_iat

pure subroutine set_at(self, idx, column, x)
! set a scalar element by index value and column name
class(DataFrame), intent(in out) :: self
integer, intent(in) :: idx
character(len=*), intent(in) :: column
real(kind=dp), intent(in) :: x
integer :: i, j
i = self%row_pos(idx)
j = self%col_pos(column)
self%values(i, j) = x
end subroutine set_at

logical function has_col(self, name)
! return .true. if dataframe has a column with the given name
class(DataFrame), intent(in) :: self
character(len=*), intent(in) :: name
integer :: j
character(len=nlen_columns) :: key
key = trim(name)
j = findloc(self%columns, key, dim=1)
has_col = (j > 0)
end function has_col

logical function has_idx(self, idx)
! return .true. if dataframe has a row with the given index value
class(DataFrame), intent(in) :: self
integer, intent(in) :: idx
integer :: i
i = findloc(self%index, idx, dim=1)
has_idx = (i > 0)
end function has_idx

function drop_cols(self, names, missing) result(df_new)
! drop columns by name
class(DataFrame), intent(in) :: self
character(len=*), intent(in) :: names(:)
character(len=*), intent(in), optional :: missing
type(DataFrame) :: df_new
logical, allocatable :: keep(:)
integer, allocatable :: ivec_keep(:)
integer :: k, j, n
character(len=100) :: miss
character(len=nlen_columns) :: key

miss = trim(default("error", missing))
miss = str_lower(miss)

n = ncol(self)
allocate(keep(n))
keep = .true.

do k = 1, size(names)
   key = trim(names(k))
   j = findloc(self%columns, key, dim=1)
   if (j <= 0) then
      if (miss == "ignore") cycle
      error stop "drop_cols: column not found: "//trim(names(k))
   end if
   keep(j) = .false.
end do

ivec_keep = pack(seq(1, n), keep)
df_new = self%icol(ivec_keep)
end function drop_cols

function drop_rows(self, idx, missing) result(df_new)
! drop rows by index value
class(DataFrame), intent(in) :: self
integer, intent(in) :: idx(:)
character(len=*), intent(in), optional :: missing
type(DataFrame) :: df_new
logical, allocatable :: keep(:)
integer, allocatable :: ivec_keep(:)
integer :: k, i, n
character(len=100) :: miss

miss = trim(default("error", missing))
miss = str_lower(miss)

n = nrow(self)
allocate(keep(n))
keep = .true.

do k = 1, size(idx)
   i = findloc(self%index, idx(k), dim=1)
   if (i <= 0) then
      if (miss == "ignore") cycle
      error stop "drop_rows: index not found"
   end if
   keep(i) = .false.
end do

ivec_keep = pack(seq(1, n), keep)
df_new = self%irow(ivec_keep)
end function drop_rows

subroutine rename_cols(self, old, new, missing)
! rename columns: replace each old(i) with new(i)
class(DataFrame), intent(in out) :: self
character(len=*), intent(in) :: old(:), new(:)
character(len=*), intent(in), optional :: missing
integer :: k, j
character(len=100) :: miss
character(len=nlen_columns) :: key

if (size(old) /= size(new)) error stop "rename_cols: size(old) /= size(new)"

miss = trim(default("error", missing))
miss = str_lower(miss)

do k = 1, size(old)
   key = trim(old(k))
   j = findloc(self%columns, key, dim=1)
   if (j <= 0) then
      if (miss == "ignore") cycle
      error stop "rename_cols: column not found: "//trim(old(k))
   end if
   self%columns(j) = trim(new(k))
end do
end subroutine rename_cols



function where_cols(self, mask_cols) result(df_new)
! keep columns where mask_cols(j) is .true.
class(DataFrame), intent(in) :: self
logical, intent(in) :: mask_cols(:)
type(DataFrame) :: df_new
integer, allocatable :: j_keep(:)
if (size(mask_cols) /= ncol(self)) error stop "where_cols: size(mask_cols) /= ncol(self)"
j_keep = pack(seq(1, ncol(self)), mask_cols)
df_new = self%icol(j_keep)
end function where_cols

function filter_cols(self, mask_cols, drop) result(df_new)
! filter columns by mask; if drop=.true. then drop columns where mask is .true.
class(DataFrame), intent(in) :: self
logical, intent(in) :: mask_cols(:)
logical, intent(in), optional :: drop
type(DataFrame) :: df_new
logical :: drop_
logical, allocatable :: keep(:)
drop_ = default(.false., drop)
if (size(mask_cols) /= ncol(self)) error stop "filter_cols: size(mask_cols) /= ncol(self)"
allocate(keep(size(mask_cols)))
if (drop_) then
   keep = .not. mask_cols
else
   keep = mask_cols
end if
df_new = self%where_cols(keep)
end function filter_cols

function where(self, mask_rows, mask_cols) result(df_new)
! keep rows and columns where masks are .true.
class(DataFrame), intent(in) :: self
logical, intent(in) :: mask_rows(:)
logical, intent(in) :: mask_cols(:)
type(DataFrame) :: df_new
integer, allocatable :: i_keep(:), j_keep(:)
if (size(mask_rows) /= nrow(self)) error stop "where: size(mask_rows) /= nrow(self)"
if (size(mask_cols) /= ncol(self)) error stop "where: size(mask_cols) /= ncol(self)"
i_keep = pack(seq(1, nrow(self)), mask_rows)
j_keep = pack(seq(1, ncol(self)), mask_cols)
df_new = DataFrame(index=self%index(i_keep), columns=self%columns(j_keep), values=self%values(i_keep, j_keep))
end function where

function filter(self, mask_rows, mask_cols, drop_rows, drop_cols) result(df_new)
! filter rows and columns by masks; if drop_rows/drop_cols are .true. then drop where mask is .true.
class(DataFrame), intent(in) :: self
logical, intent(in) :: mask_rows(:)
logical, intent(in) :: mask_cols(:)
logical, intent(in), optional :: drop_rows, drop_cols
type(DataFrame) :: df_new
logical :: drop_r, drop_c
logical, allocatable :: keep_rows(:), keep_cols(:)

if (size(mask_rows) /= nrow(self)) error stop "filter: size(mask_rows) /= nrow(self)"
if (size(mask_cols) /= ncol(self)) error stop "filter: size(mask_cols) /= ncol(self)"

drop_r = default(.false., drop_rows)
drop_c = default(.false., drop_cols)

allocate(keep_rows(size(mask_rows)))
allocate(keep_cols(size(mask_cols)))

if (drop_r) then
   keep_rows = .not. mask_rows
else
   keep_rows = mask_rows
end if

if (drop_c) then
   keep_cols = .not. mask_cols
else
   keep_cols = mask_cols
end if

df_new = self%where(keep_rows, keep_cols)
end function filter

function iloc(self, rows, cols) result(df_new)
! positional selection by row/column positions (1-based)
class(DataFrame), intent(in) :: self
integer, intent(in), optional :: rows(:)
integer, intent(in), optional :: cols(:)
type(DataFrame) :: df_new
if (present(rows) .and. present(cols)) then
   df_new = self%select(irows=rows, icols=cols)
else if (present(rows)) then
   df_new = self%select(irows=rows)
else if (present(cols)) then
   df_new = self%select(icols=cols)
else
   df_new = self%select()
end if
end function iloc

function select(self, rows, columns, irows, icols) result(df_new)
! select a sub-dataframe using label- or position-based selectors on each axis.
! rules:
!  - at most one of rows/irows may be present
!  - at most one of columns/icols may be present
class(DataFrame), intent(in) :: self
integer, intent(in), optional :: rows(:)
character(len=*), intent(in), optional :: columns(:)
integer, intent(in), optional :: irows(:)
integer, intent(in), optional :: icols(:)
type(DataFrame) :: df_new
integer, allocatable :: i_keep(:), j_keep(:)
integer :: k

if (present(rows) .and. present(irows)) error stop "select: both rows and irows are present"
if (present(columns) .and. present(icols)) error stop "select: both columns and icols are present"

if (present(rows)) then
   allocate(i_keep(size(rows)))
   do k = 1, size(rows)
      i_keep(k) = self%row_pos(rows(k))
   end do
else if (present(irows)) then
   i_keep = irows
   do k = 1, size(i_keep)
      if (i_keep(k) < 1 .or. i_keep(k) > nrow(self)) error stop "select: row position out of range"
   end do
else
   i_keep = seq(1, nrow(self))
end if

if (present(columns)) then
   allocate(j_keep(size(columns)))
   do k = 1, size(columns)
      j_keep(k) = self%col_pos(columns(k))
   end do
else if (present(icols)) then
   j_keep = icols
   do k = 1, size(j_keep)
      if (j_keep(k) < 1 .or. j_keep(k) > ncol(self)) error stop "select: column position out of range"
   end do
else
   j_keep = seq(1, ncol(self))
end if

df_new = DataFrame(index=self%index(i_keep), columns=self%columns(j_keep), values=self%values(i_keep, j_keep))
end function select
pure function str_lower(str) result(out)
! return str converted to lowercase (ASCII)
character(len=*), intent(in) :: str
character(len=len(str))      :: out
integer :: i, c
out = str
do i = 1, len(str)
   c = iachar(out(i:i))
   if (c >= iachar('A') .and. c <= iachar('Z')) out(i:i) = achar(c + 32)
end do
end function str_lower




pure function irow(df, ivec) result(df_new)
! returns a dataframe with the subset of columns in ivec(:)
class(DataFrame), intent(in) :: df
integer, intent(in) :: ivec(:)
type(DataFrame) :: df_new
df_new = DataFrame(index=df%index(ivec), columns=df%columns, values=df%values(ivec, :))
end function irow

pure subroutine set_col(df, column, values)
! append a column with specified values to DataFrame df if column is not in df,
! and set the values of that column if it is already present
class(DataFrame), intent(in out) :: df
character (len=*), intent(in) :: column
real(kind=dp), intent(in) :: values(:)
integer :: jcol
if (size(values) /= nrow(df)) error stop "in set_col, size(values) /= nrow(df)"
jcol = findloc(df%columns, column, dim=1)
if (jcol == 0) then
   call append_col(df, column, values)
else
   df%values(:,jcol) = values
end if
end subroutine set_col

pure subroutine append_col(df, column, values)
! append a column with specified values to DataFrame df
class(DataFrame), intent(in out) :: df
character (len=*), intent(in) :: column
real(kind=dp), intent(in) :: values(:)
character (len=nlen_columns) :: column_
if (size(values) /= nrow(df)) error stop "in append_col, size(values) /= nrow(df)"
column_ = column
df%columns = [df%columns, column_]
df%values  = cbind(df%values, values)
end subroutine append_col

pure subroutine append_cols(df, columns, values)
! append a column with specified values to DataFrame df
class(DataFrame), intent(in out) :: df
character (len=*), intent(in) :: columns(:)
real(kind=dp), intent(in) :: values(:,:)
character (len=nlen_columns), allocatable :: columns_(:)
if (size(values, 1) /= nrow(df)) error stop "in append_cols, size(values) /= nrow(df)"
if (size(values, 2) /= size(columns)) error stop "in append_cols, size(values, 2) /= size(columns)"
columns_ = columns
df%columns = [df%columns, columns_]
df%values  = cbind(df%values, values)
end subroutine append_cols

subroutine allocate_df(df, n1, n2, default_indices, default_columns)
type(DataFrame), intent(out) :: df
integer        , intent(in)  :: n1, n2
logical        , intent(in), optional :: default_indices, default_columns
integer :: i
allocate (df%index(n1), df%columns(n2), df%values(n1, n2))
if (default(.true., default_indices)) then
   do i=1,n1
      df%index(i) = i
   end do
end if
if (default(.true., default_columns)) then
   do i=1,n2
      write (df%columns(i), "('x',i0)") i
   end do
end if
end subroutine allocate_df

elemental function nrow(df) result(num_rows)
! return the # of rows
type(DataFrame), intent(in) :: df
integer                     :: num_rows
if (allocated(df%values)) then
   num_rows = size(df%values, 1)
else
   num_rows = -1
end if
end function nrow

elemental function ncol(df) result(num_col)
! return the # of columns
type(DataFrame), intent(in) :: df
integer                     :: num_col
if (allocated(df%values)) then
   num_col = size(df%values, 2)
else
   num_col = -1
end if
end function ncol

!------------------------------------------------------------------
! read_csv:
!
! Reads from a CSV file with the following format:
!
!      ,Col1,Col2,...
!      index1,val11,val12,...
!      index2,val21,val22,...
!
! The header row begins with an empty token (before the first comma).
!------------------------------------------------------------------
subroutine read_csv(self, filename, max_col, max_rows)
class(DataFrame), intent(inout) :: self
character(len=*), intent(in)    :: filename
integer, intent(in), optional :: max_col, max_rows
integer :: io, unit, i, j, nrows, ncols, maxlen, year, month, day
integer, allocatable :: row_index(:)
real(kind=dp), allocatable :: row_values(:,:)
real(kind=dp) :: value
character(len=1024) :: line, token
character(:), allocatable :: tokens(:)

! 1) Open the file.
open(newunit=unit, file=filename, status='old', action='read', iostat=io)
if (io /= 0) then
   print *, "Error opening file:", filename
   stop
end if

! 2) Read the header line.
read(unit, '(A)', iostat=io) line
if (io /= 0) then
   print *, "Error reading header line from:", filename
   stop
end if

call split_string(line, ",", tokens)
! The first token should be empty; remaining tokens are column names.
ncols = size(tokens) - 1
if (present(max_col)) ncols = min(ncols, max_col)
if (ncols <= 0) then
   print *, "No columns detected in header of", filename
   stop
end if

! Determine maximum length among the column name tokens.
maxlen = 0
do i = 2, size(tokens)
   maxlen = max(maxlen, len_trim(tokens(i)))
end do

! Allocate columns
allocate(self%columns(ncols))
do i = 1, ncols
   self%columns(i) = tokens(i+1)
end do

! 3) Count the remaining data lines.
nrows = 0
do
   if (present(max_rows)) then
      if (nrows >= max_rows) exit
   end if
   read(unit, '(A)', iostat=io) line
   if (io /= 0 .or. trim(line) == "") exit
   nrows = nrows + 1
end do

if (nrows == 0) then
   print *, "No data lines detected in file:", filename
   stop
end if

! 4) Rewind the file and skip the header.
rewind(unit)
read(unit, '(A)') line  ! skip header

! 5) Allocate the index and values arrays.
allocate(row_index(nrows), row_values(nrows, ncols))

! 6) Read each data row.
do i = 1, nrows
   read(unit, '(A)', iostat=io) line
   if (trim(line) == "") exit
   call split_string(line, ",", tokens)
   ! First token is the index.  Accept either an integer index or an
   ! ISO date index YYYY-MM-DD, storing dates as integer YYYYMMDD.
   if (index(tokens(1), "-") > 0) then
      token = tokens(1)
      read(token(1:4), *) year
      read(token(6:7), *) month
      read(token(9:10), *) day
      row_index(i) = 10000*year + 100*month + day
   else
      read(tokens(1), *) row_index(i)
   end if
   ! Remaining tokens are the real values.
   do j = 1, ncols
      read(tokens(j+1), *) value
      row_values(i,j) = value
   end do
end do

self%index = row_index
self%values = row_values

close(unit)
end subroutine read_csv

!------------------------------------------------------------------
! display_data:
!
! Prints the DataFrame to the screen in a CSV-like format.
! If the DataFrame has more than nrows_print observations, by default only
! the first nrows_print/2 and the last (nrows_print - nrows_print/2) rows are
! printed with an indication of omitted rows.
!
! An optional logical argument 'print_all' may be provided. If it is present
! and set to .true., then all rows are printed.
!------------------------------------------------------------------
impure elemental subroutine display_data(self, print_all, fmt_ir, fmt_header, fmt_trailer, title)
class(DataFrame), intent(in) :: self
logical, intent(in), optional :: print_all
character (len=*), intent(in), optional :: fmt_ir, fmt_header, fmt_trailer, title
integer :: total, i, n_top, n_bottom
logical :: print_all_
character (len=100) :: fmt_ir_, fmt_header_
fmt_ir_ = "(i10,*(1x,f10.4))"
if (present(fmt_ir)) fmt_ir_ = fmt_ir
fmt_header_ = "(a10,*(1x,a10))"
if (present(fmt_header)) fmt_header_ = fmt_header
print_all_ = .false.
total = size(self%index)
if (blank_line_before_display) write(*,*)
if (present(title)) write(*,"(a)") title
! Print header.
write(*,fmt_header_) "index", (trim(self%columns(i)), i=1,size(self%columns))

if (print_all_) then
   ! Print all rows.
   do i = 1, total
      write(*,fmt_ir_) self%index(i), self%values(i,:)
   end do
else
   if (total <= nrows_print) then
      ! Print all rows if total is less than or equal to nrows_print.
      do i = 1, total
         write(*,fmt_ir_) self%index(i), self%values(i,:)
      end do
   else
      ! Compute number of rows for the top and bottom parts.
      n_top = nrows_print / 2
      n_bottom = nrows_print - n_top
      ! Print first n_top rows.
      do i = 1, n_top
         write(*,fmt_ir_) self%index(i), self%values(i,:)
      end do
      ! Indicate omitted rows.
      write(*,*) "   ... (", total - nrows_print, " rows omitted) ..."
      ! Print last n_bottom rows.
      do i = total - n_bottom + 1, total
         write(*,fmt_ir_) self%index(i), self%values(i,:)
      end do
   end if
end if
if (present(fmt_trailer)) write(*,fmt_trailer)
end subroutine display_data

!------------------------------------------------------------------
! write_csv:
!
! Writes the DataFrame to a CSV file in the same format as read_csv.
!------------------------------------------------------------------
subroutine write_csv(self, filename)
class(DataFrame), intent(in) :: self
character(len=*), intent(in) :: filename
integer :: i, j, unit, io
character(len=20000) :: line, field

open(newunit=unit, file=filename, status='replace', action='write', iostat=io)
if (io /= 0) then
   print *, "Error opening output file:", filename
   stop
end if

! Write header: empty token for index, then column names.
line = ""
do j = 1, size(self%columns)
   write(field,'(",", A)') trim(self%columns(j))
   line = trim(line) // trim(field)
end do
write(unit,'(A)') trim(line)

! Write each data row.
do i = 1, size(self%index)
   write(line,'(I10)') self%index(i)
   do j = 1, size(self%columns)
      write(field,'(",", F10.4)') self%values(i,j)
      line = trim(line) // trim(field)
   end do
   write(unit,'(A)') trim(line)
end do
close(unit)
end subroutine write_csv

subroutine print_summary(self, outu, fmt_header, fmt_trailer)
type(DataFrame), intent(in) :: self
integer, intent(in), optional :: outu
character (len=*), intent(in), optional :: fmt_header, fmt_trailer
integer :: outu_, nr, nc
outu_ = default(output_unit, outu)
if (present(fmt_header)) write (outu_, fmt_header)
nr = nrow(self)
nc = ncol(self)
write(outu_, "('#rows, columns:', 2(1x,i0))") nr, nc
if (nr > 0) write(outu_, "('first, last indices:', 2(1x,i0))") &
   self%index(1), self%index(nr)
if (nc > 0) write(outu_, "('first, last columns:', 2(1x,a))") &
   trim(self%columns(1)), trim(self%columns(nc))
if (present(fmt_trailer)) write (outu_, fmt_trailer)
end subroutine print_summary

subroutine alloc(self, nr, nc)
type(DataFrame), intent(out) :: self
integer        , intent(in)  :: nr, nc
allocate (self%index(nr), self%values(nr, nc))
allocate (self%columns(nc))
end subroutine alloc

subroutine random(self, nr, nc)
type(DataFrame), intent(out) :: self
integer, intent(in) :: nr, nc
integer :: i
call alloc(self, nr, nc)
call random_number(self%values)
do i=1,nr
   self%index(i) = i
end do
do i=1,nc
   write (self%columns(i), "('C',i0)") i
end do
end subroutine random

function mult_x_df(x, df) result(res)
! return x * df
real(kind=dp)  , intent(in) :: x
type(DataFrame), intent(in) :: df
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = x*res%values
end function mult_x_df

function mult_df_x(df, x) result(res)
! return df * x
type(DataFrame), intent(in) :: df
real(kind=dp)  , intent(in) :: x
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = x*res%values
end function mult_df_x

function add_x_df(x, df) result(res)
! return x * df
real(kind=dp)  , intent(in) :: x
type(DataFrame), intent(in) :: df
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = x + res%values
end function add_x_df

function add_df_x(df, x) result(res)
! return df * x
type(DataFrame), intent(in) :: df
real(kind=dp)  , intent(in) :: x
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = res%values + x
end function add_df_x

function subtract_x_df(x, df) result(res)
! return x - df
real(kind=dp)  , intent(in) :: x
type(DataFrame), intent(in) :: df
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = x - res%values
end function subtract_x_df

function subtract_df_x(df, x) result(res)
! return df - x
type(DataFrame), intent(in) :: df
real(kind=dp)  , intent(in) :: x
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = res%values - x
end function subtract_df_x

function div_df_x(df, x) result(res)
! return df / x
real(kind=dp)  , intent(in) :: x
type(DataFrame), intent(in) :: df
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = res%values/x
end function div_df_x

function div_x_df(x, df) result(res)
! return df / x
real(kind=dp)  , intent(in) :: x
type(DataFrame), intent(in) :: df
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = x/res%values
end function div_x_df

function div_n_df(n, df) result(res)
! return n / x
integer        , intent(in) :: n
type(DataFrame), intent(in) :: df
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = n/res%values
end function div_n_df

function mult_n_df(n, df) result(res)
! return n * df
integer        , intent(in) :: n
type(DataFrame), intent(in) :: df
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = n*res%values
end function mult_n_df

function mult_df_n(df, n) result(res)
! return df * n
type(DataFrame), intent(in) :: df
integer        , intent(in) :: n
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = n*res%values
end function mult_df_n

function add_n_df(n, df) result(res)
! return n * df
integer        , intent(in) :: n
type(DataFrame), intent(in) :: df
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = n + res%values
end function add_n_df

function add_df_n(df, n) result(res)
! return df * n
type(DataFrame), intent(in) :: df
integer        , intent(in) :: n
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = res%values + n
end function add_df_n

function subtract_n_df(n, df) result(res)
! return n - df
integer        , intent(in) :: n
type(DataFrame), intent(in) :: df
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = n - res%values
end function subtract_n_df

function subtract_df_n(df, n) result(res)
! return df - n
type(DataFrame), intent(in) :: df
integer        , intent(in) :: n
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = res%values - n
end function subtract_df_n

function div_df_n(df, n) result(res)
! return df / n
integer        , intent(in) :: n
type(DataFrame), intent(in) :: df
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = res%values/n
end function div_df_n

subroutine require_unique_labels(df, who)
! error stop if df has duplicate index or duplicate column names
type(DataFrame), intent(in) :: df
character(len=*), intent(in) :: who
character(len=200) :: msg
integer :: i, j

do i = 1, nrow(df) - 1
   do j = i + 1, nrow(df)
      if (df%index(i) == df%index(j)) then
         write(msg, "(a, a)") trim(who), ": duplicate index"
         error stop msg
      end if
   end do
end do

do i = 1, ncol(df) - 1
   do j = i + 1, ncol(df)
      if (trim(df%columns(i)) == trim(df%columns(j))) then
         write(msg, "(a, a)") trim(who), ": duplicate columns"
         error stop msg
      end if
   end do
end do
end subroutine require_unique_labels

integer function find_col_trim(cols, name) result(pos)
! return position of name in cols using trim() equality, or 0 if not found
character(len=*), intent(in) :: cols(:)
character(len=*), intent(in) :: name
integer :: j
pos = 0
do j = 1, size(cols)
   if (trim(cols(j)) == trim(name)) then
      pos = j
      return
   end if
end do
end function find_col_trim

function union_int(a, b) result(c)
integer, intent(in) :: a(:), b(:)
integer, allocatable :: c(:)
integer, allocatable :: tmp(:)
integer :: n, i
allocate(tmp(size(a) + size(b)))
n = 0
do i = 1, size(a)
   n = n + 1
   tmp(n) = a(i)
end do
do i = 1, size(b)
   if (.not. any(tmp(1:n) == b(i))) then
      n = n + 1
      tmp(n) = b(i)
   end if
end do
allocate(c(n))
c = tmp(1:n)
end function union_int

function intersect_int(a, b) result(c)
integer, intent(in) :: a(:), b(:)
integer, allocatable :: c(:)
integer, allocatable :: tmp(:)
integer :: n, i
allocate(tmp(size(a)))
n = 0
do i = 1, size(a)
   if (any(b == a(i))) then
      n = n + 1
      tmp(n) = a(i)
   end if
end do
allocate(c(n))
c = tmp(1:n)
end function intersect_int

function union_cols(a, b) result(c)
character(len=nlen_columns), intent(in) :: a(:), b(:)
character(len=nlen_columns), allocatable :: c(:)
character(len=nlen_columns), allocatable :: tmp(:)
integer :: n, i
allocate(tmp(size(a) + size(b)))
n = 0
do i = 1, size(a)
   n = n + 1
   tmp(n) = a(i)
end do
do i = 1, size(b)
   if (find_col_trim(tmp(1:n), b(i)) == 0) then
      n = n + 1
      tmp(n) = b(i)
   end if
end do
allocate(c(n))
c = tmp(1:n)
end function union_cols

function intersect_cols(a, b) result(c)
character(len=nlen_columns), intent(in) :: a(:), b(:)
character(len=nlen_columns), allocatable :: c(:)
character(len=nlen_columns), allocatable :: tmp(:)
integer :: n, i
allocate(tmp(size(a)))
n = 0
do i = 1, size(a)
   if (find_col_trim(b, a(i)) /= 0) then
      n = n + 1
      tmp(n) = a(i)
   end if
end do
allocate(c(n))
c = tmp(1:n)
end function intersect_cols

function aligned_binary(self, other, op, how, fill_value) result(res)
! pandas-like aligned arithmetic on union/intersection of index and columns
class(DataFrame), intent(in) :: self
type(DataFrame), intent(in)  :: other
character(len=*), intent(in) :: op
character(len=*), intent(in), optional :: how
real(kind=dp), intent(in), optional :: fill_value
type(DataFrame) :: res

character(len=20) :: how0
integer, allocatable :: idx_out(:)
character(len=nlen_columns), allocatable :: col_out(:)
real(kind=dp), allocatable :: a(:,:), b(:,:)
real(kind=dp) :: fill
logical :: do_fill
integer :: i, j, ii, jj, n1, n2
real(kind=dp) :: x

call require_unique_labels(self, "aligned_binary self")
call require_unique_labels(other, "aligned_binary other")

how0 = trim(default("outer", how))
select case (how0)
case ("outer")
   idx_out = union_int(self%index, other%index)
   col_out = union_cols(self%columns, other%columns)
case ("inner")
   idx_out = intersect_int(self%index, other%index)
   col_out = intersect_cols(self%columns, other%columns)
case ("left")
   idx_out = self%index
   col_out = self%columns
case ("right")
   idx_out = other%index
   col_out = other%columns
case default
   error stop "aligned_binary: how must be outer/inner/left/right"
end select

n1 = size(idx_out)
n2 = size(col_out)

do_fill = present(fill_value)
if (do_fill) then
   fill = fill_value
else
   fill = ieee_value(0.0_dp, ieee_quiet_nan)
end if

allocate(a(n1, n2), b(n1, n2))
a = fill
b = fill

! place self values into aligned array
do i = 1, nrow(self)
   ii = findloc(idx_out, self%index(i), dim=1)
   if (ii <= 0) cycle
   do j = 1, ncol(self)
      jj = find_col_trim(col_out, self%columns(j))
      if (jj <= 0) cycle
      x = self%values(i, j)
      if (do_fill) then
         if (ieee_is_nan(x)) x = fill
      end if
      a(ii, jj) = x
   end do
end do

! place other values into aligned array
do i = 1, nrow(other)
   ii = findloc(idx_out, other%index(i), dim=1)
   if (ii <= 0) cycle
   do j = 1, ncol(other)
      jj = find_col_trim(col_out, other%columns(j))
      if (jj <= 0) cycle
      x = other%values(i, j)
      if (do_fill) then
         if (ieee_is_nan(x)) x = fill
      end if
      b(ii, jj) = x
   end do
end do

allocate(res%index(n1), res%columns(n2), res%values(n1, n2))
res%index = idx_out
res%columns = col_out

select case (trim(op))
case ("+")
   res%values = a + b
case ("-")
   res%values = a - b
case ("*")
   res%values = a * b
case ("/")
   res%values = a / b
case default
   error stop "aligned_binary: invalid op"
end select
end function aligned_binary

function add(self, other, how, fill_value) result(res)
class(DataFrame), intent(in) :: self
type(DataFrame), intent(in) :: other
character(len=*), intent(in), optional :: how
real(kind=dp), intent(in), optional :: fill_value
type(DataFrame) :: res
res = aligned_binary(self, other, "+", how, fill_value)
end function add

function subtract(self, other, how, fill_value) result(res)
class(DataFrame), intent(in) :: self
type(DataFrame), intent(in) :: other
character(len=*), intent(in), optional :: how
real(kind=dp), intent(in), optional :: fill_value
type(DataFrame) :: res
res = aligned_binary(self, other, "-", how, fill_value)
end function subtract

function multiply(self, other, how, fill_value) result(res)
class(DataFrame), intent(in) :: self
type(DataFrame), intent(in) :: other
character(len=*), intent(in), optional :: how
real(kind=dp), intent(in), optional :: fill_value
type(DataFrame) :: res
res = aligned_binary(self, other, "*", how, fill_value)
end function multiply

function divide(self, other, how, fill_value) result(res)
class(DataFrame), intent(in) :: self
type(DataFrame), intent(in) :: other
character(len=*), intent(in), optional :: how
real(kind=dp), intent(in), optional :: fill_value
type(DataFrame) :: res
res = aligned_binary(self, other, "/", how, fill_value)
end function divide


pure function shift(self, periods, fill_value) result(df_new)
! shift the values by 'periods' rows (positive periods shifts down).
class(DataFrame), intent(in) :: self
integer, intent(in), optional :: periods
real(kind=dp), intent(in), optional :: fill_value
type(DataFrame) :: df_new
integer :: p, k, nr, nc
real(kind=dp) :: fill

p = default(1, periods)
fill = default(ieee_value(0.0_dp, ieee_quiet_nan), fill_value)

df_new = self
nr = nrow(self)
nc = ncol(self)
if (nr <= 0 .or. nc <= 0) return

df_new%values = fill
if (p == 0) then
   df_new%values = self%values
else if (p > 0) then
   if (p < nr) df_new%values(p+1:nr,:) = self%values(1:nr-p,:)
else
   k = -p
   if (k < nr) df_new%values(1:nr-k,:) = self%values(k+1:nr,:)
end if
end function shift

pure function pct_change(self, periods) result(df_new)
! percent change (simple return) over 'periods' rows.
class(DataFrame), intent(in) :: self
integer, intent(in), optional :: periods
type(DataFrame) :: df_new
type(DataFrame) :: lag
integer :: p, nr, nc

p = default(1, periods)
lag = self%shift(p)  ! default fill is NaN

df_new%index = self%index
df_new%columns = self%columns
nr = nrow(self)
nc = ncol(self)
allocate(df_new%values(nr, nc))
if (nr == 0 .or. nc == 0) return

df_new%values = self%values/lag%values - 1.0_dp
end function pct_change

pure function log_change(self, periods) result(df_new)
! log change (log return) over 'periods' rows: ln(x(t)/x(t-periods)).
class(DataFrame), intent(in) :: self
integer, intent(in), optional :: periods
type(DataFrame) :: df_new
type(DataFrame) :: lag
integer :: p, nr, nc
real(kind=dp), allocatable :: ratio(:,:)
real(kind=dp) :: nan

p = default(1, periods)
lag = self%shift(p)  ! default fill is NaN

df_new%index = self%index
df_new%columns = self%columns
nr = nrow(self)
nc = ncol(self)
allocate(df_new%values(nr, nc))
nan = ieee_value(0.0_dp, ieee_quiet_nan)
df_new%values = nan
if (nr == 0 .or. nc == 0) return

allocate(ratio(nr, nc))
ratio = self%values/lag%values
where (ratio > 0.0_dp .and. .not. ieee_is_nan(ratio))
   df_new%values = log(ratio)
end where
end function log_change

pure function reindex(self, new_index, method, fill_value) result(df_new)
! return a dataframe with index replaced by new_index and values reindexed.
! method can be: "none" (exact), "ffill" (forward fill), "bfill" (back fill).
class(DataFrame), intent(in) :: self
integer, intent(in) :: new_index(:)
character(len=*), intent(in), optional :: method
real(kind=dp), intent(in), optional :: fill_value
type(DataFrame) :: df_new
character(len=10) :: method_
integer :: i, j, nr_old, nc, pos
real(kind=dp) :: fill
logical :: old_sorted

method_ = "none"
if (present(method)) method_ = adjustl(method)
fill = default(ieee_value(0.0_dp, ieee_quiet_nan), fill_value)

nr_old = nrow(self)
nc = ncol(self)

df_new%index = new_index
df_new%columns = self%columns
allocate(df_new%values(size(new_index), nc))
df_new%values = fill
if (size(new_index) == 0 .or. nc == 0) return
if (nr_old == 0) return

old_sorted = .true.
do i=2,nr_old
   if (self%index(i) < self%index(i-1)) then
      old_sorted = .false.
      exit
   end if
end do

do i = 1, size(new_index)
   select case (trim(method_))
   case ("none")
      if (old_sorted) then
         pos = bsearch_exact(self%index, new_index(i))
      else
         pos = findloc(self%index, new_index(i), dim=1)
      end if
   case ("ffill")
      if (old_sorted) then
         pos = bsearch_ffill(self%index, new_index(i))
      else
         pos = 0
         do j=1,nr_old
            if (self%index(j) <= new_index(i)) then
               if (pos == 0 .or. self%index(j) > self%index(pos)) pos = j
            end if
         end do
      end if
   case ("bfill")
      if (old_sorted) then
         pos = bsearch_bfill(self%index, new_index(i))
      else
         pos = 0
         do j=1,nr_old
            if (self%index(j) >= new_index(i)) then
               if (pos == 0 .or. self%index(j) < self%index(pos)) pos = j
            end if
         end do
      end if
   case default
      error stop "in reindex, invalid method"
   end select

   if (pos > 0) df_new%values(i,:) = self%values(pos,:)
end do

contains

pure integer function bsearch_exact(a, x) result(pos)
integer, intent(in) :: a(:)
integer, intent(in) :: x
integer :: lo, hi, mid
pos = 0
lo = 1
hi = size(a)
do while (lo <= hi)
   mid = (lo + hi)/2
   if (a(mid) == x) then
      pos = mid
      return
   else if (a(mid) < x) then
      lo = mid + 1
   else
      hi = mid - 1
   end if
end do
end function bsearch_exact

pure integer function bsearch_ffill(a, x) result(pos)
! rightmost a(pos) <= x
integer, intent(in) :: a(:)
integer, intent(in) :: x
integer :: lo, hi, mid
pos = 0
lo = 1
hi = size(a)
do while (lo <= hi)
   mid = (lo + hi)/2
   if (a(mid) <= x) then
      pos = mid
      lo = mid + 1
   else
      hi = mid - 1
   end if
end do
end function bsearch_ffill

pure integer function bsearch_bfill(a, x) result(pos)
! leftmost a(pos) >= x
integer, intent(in) :: a(:)
integer, intent(in) :: x
integer :: lo, hi, mid
pos = 0
lo = 1
hi = size(a)
do while (lo <= hi)
   mid = (lo + hi)/2
   if (a(mid) >= x) then
      pos = mid
      hi = mid - 1
   else
      lo = mid + 1
   end if
end do
end function bsearch_bfill

end function reindex


subroutine require_same_df(df0, df1, who)
type(DataFrame), intent(in) :: df0, df1
character(len=*), intent(in) :: who
character(len=200) :: msg
integer :: j
if (nrow(df0) /= nrow(df1)) then
   write(msg,'("in ",a,", nrow mismatch")') trim(who)
   error stop msg
end if
if (ncol(df0) /= ncol(df1)) then
   write(msg,'("in ",a,", ncol mismatch")') trim(who)
   error stop msg
end if
if (any(df0%index /= df1%index)) then
   write(msg,'("in ",a,", index mismatch")') trim(who)
   error stop msg
end if
do j=1,ncol(df0)
   if (trim(df0%columns(j)) /= trim(df1%columns(j))) then
      write(msg,'("in ",a,", columns mismatch")') trim(who)
      error stop msg
   end if
end do
end subroutine require_same_df

function add_df_df(df0, df1) result(res)
type(DataFrame), intent(in) :: df0
type(DataFrame), intent(in) :: df1
type(DataFrame)             :: res
call require_same_df(df0, df1, "add_df_df")
res = df0
if (allocated(res%values)) res%values = df0%values + df1%values
end function add_df_df

function subtract_df_df(df0, df1) result(res)
type(DataFrame), intent(in) :: df0
type(DataFrame), intent(in) :: df1
type(DataFrame)             :: res
call require_same_df(df0, df1, "subtract_df_df")
res = df0
if (allocated(res%values)) res%values = df0%values - df1%values
end function subtract_df_df

function mult_df_df(df0, df1) result(res)
type(DataFrame), intent(in) :: df0
type(DataFrame), intent(in) :: df1
type(DataFrame)             :: res
call require_same_df(df0, df1, "mult_df_df")
res = df0
if (allocated(res%values)) res%values = df0%values * df1%values
end function mult_df_df

function div_df_df(df0, df1) result(res)
type(DataFrame), intent(in) :: df0
type(DataFrame), intent(in) :: df1
type(DataFrame)             :: res
call require_same_df(df0, df1, "div_df_df")
res = df0
if (allocated(res%values)) res%values = df0%values / df1%values
end function div_df_df



elemental function power_df_n(df, n) result(res)
! return df**n element-wise
integer        , intent(in) :: n
type(DataFrame), intent(in) :: df
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = res%values**n
end function power_df_n

elemental function power_df_x(df, x) result(res)
! return df**x element-wise
real(kind=dp), intent(in)   :: x
type(DataFrame), intent(in) :: df
type(DataFrame)             :: res
res = df
if (allocated(res%values)) res%values = res%values**x
end function power_df_x

elemental function subset_stride(df, stride) result(df_new)
type(DataFrame), intent(in) :: df
integer, intent(in) :: stride
type(DataFrame) :: df_new
! print*,"df%index(1:nrow(df):stride)", df%index(1:nrow(df):stride)
! print*,"df%values(1:nrow(df):stride, :)", df%values(1:nrow(df):stride, :)
! in the line below, some parentheses are added to work around
! compiler bugs
if (stride == 0) error stop "in subset_stride, stride must nost equal 0"
df_new = DataFrame(index=(df%index(1:nrow(df):stride)), &
   columns=df%columns, values = (df%values(1:nrow(df):stride, :)))
end function subset_stride
end module dataframe_mod
program xdataframe
use dataframe_mod, only: DataFrame
implicit none

type(DataFrame) :: df
character(len=*), parameter :: fname_in  = "spy_tlt.csv"
character(len=*), parameter :: fname_out = "output.csv"
logical, parameter :: display_all = .false.

! Read the CSV file into the DataFrame.
call df%read_csv(fname_in, 1000, 10)

! Display the DataFrame using default behavior (first and last nrows_print/2 rows).
print *, "Contents of the DataFrame (default view from file:", fname_in, "):"
call df%display()
call df%display(.false., "(i10,*(1x,f8.2))", &
   "(a10,*(1x,a8))", "()", "with non-default formats")

if (display_all) then
   ! Display the full DataFrame by passing .true. to print_all.
   print *, "Full contents of the DataFrame (from file:", fname_in, "):"
   call df%display(print_all=.true.)
end if

! Write the DataFrame out to another CSV file.
call df%write_csv(fname_out)
print *, "DataFrame written to:", fname_out

end program xdataframe
